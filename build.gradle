/*
DEPENDENCIES
 */

plugins {
    id "nebula.os-package" version "2.0.3"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'application'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile group: 'log4j', name: 'log4j', version:'1.2.17'
    compile group: 'org.apache.thrift', name: 'libthrift', version:'0.9.2'
    compile group: 'org.slf4j', name: 'slf4j-api', version:'1.7.7'
    compile group: 'org.slf4j', name: 'slf4j-simple', version:'1.7.7'
    compile group: 'edu.stanford.nlp', name: 'stanford-corenlp', version:'3.4.1'
    compile group: 'edu.stanford.nlp', name: 'stanford-corenlp', version:'3.4.1', classifier:'models'
    testCompile group: 'junit', name: 'junit', version:'3.8.1'
}

/*
HELPERS
 */

def gitVersion = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    def version = new File('version')
    version.write(stdout.toString().trim())
    return stdout.toString().trim()
}

def gitChangelog = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'tag', '-n5'
        standardOutput = stdout
    }
    def version = new File('build/debian/changelog')
    version.write(stdout.toString().trim())
    return stdout.toString().trim()
}

/*
CONFIGURATION
 */

group = 'com.knowsis.stanfordthrift'

description = """stanford-corenlp-wrapper"""

sourceCompatibility = 1.5
targetCompatibility = 1.5

def thrift = [
    config: 'corenlp.thrift',
    pythonOut: 'build/thrift/py/knowsis'
]

version = gitVersion()

sourceSets {
    main {
        java { srcDir 'src' }
        resources { srcDir 'lib' }
    }
}

/*
TASKS
 */

clean {
    delete 'src/CoreNLP'
    delete 'build'
}

task dirs << {
    new File(thrift.pythonOut).mkdirs()
    new File('src/CoreNLP').mkdirs()
}

task genThriftPython (type: Exec, dependsOn: 'dirs') {
    commandLine 'thrift', '-v', '--gen', 'py:utf8strings,slots,new_style', '-out', thrift.pythonOut, thrift.config
}

task genThriftJava (type: Exec) {
    commandLine 'thrift', '-v', '--gen', 'java', '-out', 'src', thrift.config
}

compileJava {
    dependsOn 'genThriftJava', 'genThriftPython'
}

task uberJar(type: Jar, dependsOn: [':compileJava', ':processResources']) {
    from files(sourceSets.main.output.classesDir)
    from files(sourceSets.main.output.resourcesDir)
    from configurations.runtime.asFileTree.files.collect { zipTree(it) }

    manifest {
        attributes 'Main-Class': 'org.ets.research.nlp.stanford_thrift.StanfordCoreNLPServer'
    }
}

task packagePython(dependsOn: 'genThriftPython') << {
    copy {
        from 'setup.py'
        from 'MANIFEST.in'
        from 'version'
        into 'build/thrift/py'
    }
    exec {
        executable = 'python'
        workingDir = 'build/thrift/py'
        args = ['setup.py', 'sdist']
    }
}

task packageDeb(type: Deb, dependsOn: 'uberJar') {
    summary 'knowsis stanford corenlp wrapper with apache thrift'
    packageGroup 'misc'

    packager 'dev@knowsis.com'
    url 'www.knowsis.com'

    user 'knowsis'

    into '/home/knowsis/corenlp'

    from('build/libs') {
        into 'lib'
        user 'knowsis'
    }
}

defaultTasks 'clean', 'packagePython', 'packageDeb'
